//---------------------------------------------------------------------
//  Globals
//---------------------------------------------------------------------

globals
{	
	// gateway for everything except out internal phones:
	//
	gateway_host=192.168.1.109;
	gateway_prefix=0;             // if 0... was dialed internally to get
	                              // an outside line, what should the prefix
	                              // be to the gateway?
	// max. forwards from user to user to prevent infinite loops:
	//
	max_forwards=10;
	
	// special emergency numbers:
	//
	emergency_police=110;
	emergency_fire=112;
};


//---------------------------------------------------------------------
//  User Login Macro
//---------------------------------------------------------------------

macro cmm-login( new_user_name )
{	
	Answer();
	AGI(/opt/ast/dialplan-scripts/cmm-check-user.agi|${new_user_name});
	NoOp(### AGI returned: ret = ${ret});
	if ("${ret}" != "1")
		goto unknown;
	
	Wait(1);
	Read(pass,agent-pass,10,skip,3,5);
	AGI(/opt/ast/dialplan-scripts/cmm-login.agi|${new_user_name}|${pass}|${user_id});
	NoOp(### AGI returned: ret = ${ret});
	if ("${ret}" = "error")
		goto error;
	if ("${ret}" != "ok")
		goto unknown;
	
	Playback(agent-loginok);
	TrySystem(asterisk -rx \'sip notify snom-reboot ${new_user_name}\');
	TrySystem(asterisk -rx \'sip notify snom-reboot ${user_name}\');
	TrySystem(asterisk -rx \'sip prune realtime ${new_user_name}\');
	TrySystem(asterisk -rx \'sip prune realtime ${user_name}\');
	Hangup();
	
	unknown:
		Wait(1);
		Playback(vm-incorrect&vm-goodbye);
		Wait(1);
		Hangup();
	
	error:
		Playback(tt-somethingwrong);
		Wait(1);
		Hangup();
}


//---------------------------------------------------------------------
//  User Logout Macro
//---------------------------------------------------------------------

macro cmm-logout()
{	
	Answer();
	Wait(1);
	AGI(/opt/ast/dialplan-scripts/cmm-logout.agi|${user_id});
	Playback(agent-loggedoff&vm-goodbye);
	Wait(1);
	TrySystem(asterisk -rx \'sip notify snom-reboot ${user_name}\');
	TrySystem(asterisk -rx \'sip prune realtime ${user_name}\');
	Hangup();	
}


//---------------------------------------------------------------------
//  User Login/Logout Context
//---------------------------------------------------------------------

context cmm-login
{
	// login:
	_*0. => &cmm-login(${EXTEN:2});
	// logout:
	*0*  => &cmm-logout();
}


//---------------------------------------------------------------------
//  Voicemailbox
//---------------------------------------------------------------------

// Voicemail abfragen mit eigener Mailbox

context voicemail-self
{
	mailbox => {
		Set(language=de);
		VoiceMailMain(${user_name},s);
	}
	asterisk  => Goto(mailbox,1);
	voicemail => Goto(mailbox,1);
	80        => Goto(mailbox,1);
}

// Voicemail abfragen mit beliebieger Mailbox

context voicemail-any
{	
	81 => {
		Set(language=de);
		VoiceMailMain();
	}
}


//---------------------------------------------------------------------
//  Macro to Record VM Box Announcements
//---------------------------------------------------------------------

macro vm-record( source )
{
	if ("${source}" = "internal" || "${source}" = "external") {
		Answer();
		Wait(1);
		Playback(vm-extension);
		SayDigits(${user_name});
		Playback(vm-rec-unv);
		Record(/opt/ast/vm-rec/${user_name}-${source}.alaw,5,300);
		Playback(vm-msgsaved);
		Playback(/opt/ast/vm-rec/${user_name}-${source});
		Wait(1);
	}
	Hangup();
}


//---------------------------------------------------------------------
//  Record VM Box Announcements
//---------------------------------------------------------------------

context vm-ann-rec
{
	*99* => {
		&vm-record(internal);
		Hangup();
	}
	*98* => {
		&vm-record(external);
		Hangup();
	}
}
#include "e-test.ael";


//---------------------------------------------------------------------
//  To Internal Phones
//---------------------------------------------------------------------

context to-internal-nobody
{
	_9XXXX => {
		Dial(SIP/${EXTEN},30,tT);
		Hangup();
	}
}







