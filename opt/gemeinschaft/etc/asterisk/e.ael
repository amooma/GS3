//---------------------------------------------------------------------
//            Gemeinschaft - asterisk cluster gemeinschaft
// 
// $Revision$
// 
// Copyright 2007, amooma GmbH, Bachstr. 126, 56566 Neuwied, Germany,
// http://www.amooma.de/
// Stefan Wintermeyer <stefan.wintermeyer@amooma.de>
// Philipp Kempgen <philipp.kempgen@amooma.de>
// Peter Kozak <peter.kozak@amooma.de>
// 
// This program is free software; you can redistribute it and/or
// modify it under the terms of the GNU General Public License
// as published by the Free Software Foundation; either version 2
// of the License, or (at your option) any later version.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
// 
// You should have received a copy of the GNU General Public License
// along with this program; if not, write to the Free Software
// Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston,
// MA 02110-1301, USA.
//---------------------------------------------------------------------


//---------------------------------------------------------------------
//  Globals
//---------------------------------------------------------------------

globals {
	#include "/opt/gemeinschaft/etc/asterisk/e-globals.ael"
};

//---------------------------------------------------------------------
// Macro to emulate SIPRemoveHeader() if absent
//---------------------------------------------------------------------

macro sip-remove-header( sipremoveheader_arg ) {
	// SIPRemoveHeader() was added in Asterisk trunk rev. 168638, branch 1.6.2, tag 1.6.2.0-beta1 (https://issues.asterisk.org/view.php?id=14246)
	// http://lists.digium.com/pipermail/asterisk-dev/2009-January/036102.html
	// https://issues.asterisk.org/view.php?id=14246
	TryExec(SIPRemoveHeader(${sipremoveheader_arg}));
	if ("${TRYSTATUS}" = "NOAPP") {
		Verbose(2,### Emulating SIPRemoveHeader ...);
		if ("${sipremoveheader_arg}" = "") {
			// Remove all custom headers (
			for (i=1; ${i}<=99; i=${i}+1) {
				Set(sipaddheader_varname=SIPADDHEADER${SPRINTF(%02d,${i})});
				if (${LEN(${${sipaddheader_varname}})} > 0) {
					//Verbose(2,### ${sipaddheader_varname} = ${${sipaddheader_varname}});
					Set(${sipaddheader_varname}=);
				}
			}
		}
		else {
			//Verbose(1,### Emulation of SIPRemoveHeader with arguments not implemented!);
			// Remove some headers
			Verbose(2,### Emulating SIPRemoveHeader: removing "${sipremoveheader_arg}" ...);
			AGI(/opt/gemeinschaft/dialplan-scripts/sip-remove-header.agi,${sipremoveheader_arg});
		}
	}
	return;
}

//---------------------------------------------------------------------
//  Macro to get or generate a "connection ID"
//---------------------------------------------------------------------

macro pass-connid() {
	// generates a "connection ID" to be used in a custom X-Org-ConnID header.
	// sets ${connid}.
	if ("${connid_enabled}" = "1") {
		if ("${connid}" = "") {
			if ("${CHANNEL(channeltype)}" = "SIP") {
				if ("${SIP_HEADER(X-Org-ConnID)}" != "") {
					Set(__connid=${SIP_HEADER(X-Org-ConnID)});
					Verbose(1,### Received ConnID: ${connid});
				}
			}
			if ("${connid}" = "") {
				Set(__connid=${STRFTIME(,,%Y%m%d-%H%M%S)}-${CUT(UNIQUEID,.,2-)}-${system_ip}-${RAND(100000000,999999999)});
				// e.g. 20080923-080136-5-192.168.1.130-380845309
				Verbose(1,### Generated ConnID: ${connid});
			}
		}
		Set(CDR(x_connid)=${connid});
		Set(__SIPADDHEADER_connid=X-Org-ConnID: ${connid});
	}
	return;
}


//---------------------------------------------------------------------
//  Macro to signal dialstatus
//---------------------------------------------------------------------

macro hangup-proper-status() {
	Verbose(1,### DIALSTATUS: ${DIALSTATUS});
	if ("${DIALSTATUS}" = "BUSY") {
		//Busy(3);
		// 17 = AST_CAUSE_USER_BUSY => SIP 486 Busy here
		Set(PRI_CAUSE=17);
		Hangup(17);
	} else if ("${DIALSTATUS}" = "CHANUNAVAIL") {
		//Congestion(3);
		// 27 = AST_CAUSE_DESTINATION_OUT_OF_ORDER => SIP 502 Bad Gateway
		Set(PRI_CAUSE=27);
		Hangup(27);
	} else if ("${DIALSTATUS}" = "CONGESTION") {
		//Congestion(3);
		// 34 = AST_CAUSE_CONGESTION => SIP 503 Service Unavailable
		Set(PRI_CAUSE=34);
		Hangup(34);
	}
	Hangup();
	return;
}

macro hangup-proper-status-urldial() {
	Verbose(1,### DIALSTATUS: ${DIALSTATUS});
	if ("${DIALSTATUS}" = "BUSY") {
		Busy();
		Hangup();
	} else if ("${DIALSTATUS}" = "CHANUNAVAIL") {
		Congestion();
		Hangup();
	} else if ("${DIALSTATUS}" = "CONGESTION") {
		Congestion();
		Hangup();
	}
	Hangup();
	return;
}


//---------------------------------------------------------------------
//  check if a certain number should be treated as internal even
//  if it's not internal to Gemeinschaft
//---------------------------------------------------------------------

macro is_dmz( is_dmz_num ) {
	// callerid(num) for incoming calls or dialed number for outgoing
	//if ("${is_dmz_num:0:1}" = "0" || "${is_dmz_num:0:1}" = "" || "${is_dmz_num:0:2}" = "60") {
	//	// from PSTN or branch office => external
	if ("${is_dmz_num:0:1}" = "0" || "${is_dmz_num:0:1}" = "") {
		Set(is_dmz=);
	}
	else {
		// everything else (even from the gateway) => internal
		Set(is_dmz=1);
	}
	return;
}


//---------------------------------------------------------------------
//  User Login Macro
//---------------------------------------------------------------------

macro cmm-login( new_user_name ) {
	
	Set(CDR(amaflags)=OMIT);
	Answer();
	AGI(/opt/gemeinschaft/dialplan-scripts/cmm-check-user.agi,${new_user_name},${is_from_gateway});
	//NoOp(### AGI returned: ret = ${ret});
	if ("${ret}" != "1")
		goto unknown;
	
	Wait(1);
	Read(pass,agent-pass,10,s,3,5);
	AGI(/opt/gemeinschaft/dialplan-scripts/cmm-login.agi,${new_user_name},${pass},${user_id},${is_from_gateway},${CALLERID(num)});
	//NoOp(### AGI returned: ret = ${ret});
	if ("${ret}" = "error")
		goto error;
	if ("${ret}" != "ok")
		goto unknown;
	
	//Playback(agent-loginok);
	UserEvent(UserLoginUI,user: ${new_user_name});
	Playback(/opt/gemeinschaft/sounds/${gs_lang}/sie-sind-jetzt-angemeldet);
	Playback(/opt/gemeinschaft/sounds/${gs_lang}/bitte-warten-sie);
	TrySystem(/opt/gemeinschaft/dialplan-scripts/prov-sync-phone --ext='${new_user_name}');
	if ("${user_name}" != "${new_user_name}") {
		if ("${user_name}" != "") {
			TrySystem(/opt/gemeinschaft/dialplan-scripts/prov-sync-phone --ext='${user_name}');
		} else {
			TrySystem(/opt/gemeinschaft/dialplan-scripts/prov-sync-phone --ext='${CALLERID(num)}');
		}
	}
	if ("${new_phone_ip}" != "") {
		TrySystem(/opt/gemeinschaft/dialplan-scripts/prov-sync-phone --ip='${new_phone_ip}');
	}
	if ("${old_phone_ip}" != "" && "${old_phone_ip}" != "${new_phone_ip}") {
		TrySystem(/opt/gemeinschaft/dialplan-scripts/prov-sync-phone --ip='${old_phone_ip}');
	}
	if ("${CHANNEL(channeltype)}" = "SIP") {
		TrySystem(/opt/gemeinschaft/dialplan-scripts/prov-sync-phone --ip='${SIPCHANINFO(peerip)}');
		if ("${SIPCHANINFO(recvip)}" != "${SIPCHANINFO(peerip)}") {
			TrySystem(/opt/gemeinschaft/dialplan-scripts/prov-sync-phone --ip='${SIPCHANINFO(recvip)}');
		}
	}
	
	Hangup();
	return;
	
	unknown:
		Wait(1);
		Playback(vm-incorrect&vm-goodbye);
		Wait(0.5);
		Hangup();
		return;
	
	error:
		Playback(tt-somethingwrong);
		Wait(0.5);
		Hangup();
		return;

	return;
}


//---------------------------------------------------------------------
//  User Logout Macro
//---------------------------------------------------------------------

macro cmm-logout() {
	
	Set(CDR(amaflags)=OMIT);
	Answer();
	Wait(0.5);
	AGI(/opt/gemeinschaft/dialplan-scripts/cmm-logout.agi,${user_id},${is_from_gateway},${CALLERID(num)});
	if ("${ret}" = "error")
		goto error;
	//Playback(agent-loggedoff&vm-goodbye);
	UserEvent(UserLogoffUI,user: ${user_name});
	Playback(/opt/gemeinschaft/sounds/${gs_lang}/sie-sind-abgemeldet);
	Playback(/opt/gemeinschaft/sounds/${gs_lang}/bitte-warten-sie);
	if ("${user_name}" != "") {
		TrySystem(/opt/gemeinschaft/dialplan-scripts/prov-sync-phone --ext='${user_name}');
	} else {
		TrySystem(/opt/gemeinschaft/dialplan-scripts/prov-sync-phone --ext='${CALLERID(num)}');
	}
	if ("${old_phone_ip}" != "") {
		TrySystem(/opt/gemeinschaft/dialplan-scripts/prov-sync-phone --ip='${old_phone_ip}');
	}
	if ("${CHANNEL(channeltype)}" = "SIP") {
		if ("${SIPCHANINFO(peerip)}" != "${old_phone_ip}") {
			TrySystem(/opt/gemeinschaft/dialplan-scripts/prov-sync-phone --ip='${SIPCHANINFO(peerip)}');
		}
		if ("${SIPCHANINFO(recvip)}" != "${old_phone_ip}" && "${SIPCHANINFO(recvip)}" != "${SIPCHANINFO(peerip)}") {
			TrySystem(/opt/gemeinschaft/dialplan-scripts/prov-sync-phone --ip='${SIPCHANINFO(recvip)}');
		}
	}
	
	Hangup();
	return;

	error:
		Playback(tt-somethingwrong);
		Wait(0.5);
		Hangup();
		return;

	return;
}


//---------------------------------------------------------------------
//  User Login/Logout context
//---------------------------------------------------------------------

context cmm {
	
	*0*  => &cmm-logout();
	_*0. => &cmm-login(${EXTEN:2});
}


//---------------------------------------------------------------------
//  Agent Login Macro
//---------------------------------------------------------------------

macro agent-login( new_user_name ) {

	Answer();
	NoOp(### Agent ${new_user_name} tries to log in on user: ret = ${user_name});
	AGI(/opt/gemeinschaft/dialplan-scripts/agent-needpw.agi,${new_user_name});

	if ( "${agent_needpw}" = "1" ) {
		Wait(1);
		Read(pass,agent-pass);
	}

	AGI(/opt/gemeinschaft/dialplan-scripts/agent-login.agi,${new_user_name},${pass},${user_name});
	if ("${ret}" = "error")
		goto error;
	if ("${ret}" != "ok")
		goto unknown;
	
	//we will first have to log off the agent from the phone he is registered at, if there is any
	if("${old_user}"  != "") {
        AGI(/opt/gemeinschaft/dialplan-scripts/agent-login-logout.agi,${old_user},${new_user_name},logoutall);
		UserEvent(QueueLogoffUI,user: ${old_user},queue: *);
		for(i=0;${i} < ${oqueue_count}; i=${i} + 1){
			queueNr=${oqueue${i}};
			QueueLog(${queueNr},${UNIQUEID},Local/${new_user_name}@agents,REMOVEMEMBER,SIP/${user_name});
			UserEvent(AgentLogoffUI,agent: ${new_user_name});
		}
	}


	if("${old_local_agent}" != ""){
		AGI(/opt/gemeinschaft/dialplan-scripts/agent-login-logout.agi,${user_name},${old_local_agent},logoutall);
		UserEvent(QueueLogoffUI,user: ${user_name},queue: *);
		for(i=0;${i} < ${oqueue_count}; i=${i} + 1){
			queueNr=${oqueue${i}};
			QueueLog(${queueNr},${UNIQUEID},Local/${old_local_agent}@agents,REMOVEMEMBER,SIP/${user_name});
		}
		UserEvent(AgentLogoffUI,agent: ${old_local_agent});
	}

	if( ${old_local_user} > 0 ) {
		&queue-logout-all-silent();
		//user logoff
	}

	//Logoff the agent, if he is still registered on another phone

	// Login the agent
	// NoOp(### Anz. queues: ${queue_count});
	AGI(/opt/gemeinschaft/dialplan-scripts/agent-login-logout.agi,${user_name},${new_user_name},login);
	NoOp(### AGI returned: ${agent_login_status});
	if( "${agent_login_status}" == "OK"){ 
	
		UserEvent(AgentLoginUI,agent: ${new_user_name},user: ${user_name});
		for(i=0;${i} < ${queue_count}; i=${i} + 1){
			queueNr=${queue${i}};
			UserEvent(QueueLoginUI,user: ${user_name},queue: ${queueNr});
			QueueLog(${queueNr},${UNIQUEID},Local/${new_user_name}@agents,ADDMEMBER,SIP/${user_name});
		} 
		Playback(agent-loginok);

	}
	else
		goto error;
	Hangup();
	
	unknown:
		Wait(1);
		Playback(vm-incorrect&vm-goodbye);
		Wait(0.5);
		Hangup();
	
	error:
		Playback(tt-somethingwrong);
		Wait(0.5);
		Hangup();

	return;
}

macro agent-login-enterid() {
	
	Answer();
	NoOp(### Agent without id tries to log in);
	Wait(1);
	Read(new_user_name,/opt/gemeinschaft/sounds/${gs_lang}/agent-enterid,10,s,3,5);
	&agent-login(${new_user_name});
	return;

}

macro agent-logout() {
	
	Answer();
	Verbose(1,### Agent on ${user_name} wants to log out from all queues);
	AGI(/opt/gemeinschaft/dialplan-scripts/agent-login-logout.agi,${user_name},,logoutall);
	Wait(0.5);
	if ("${agent_login_status}" = "loggedout") {
		
		
		UserEvent(AgentLogoffUI,agent: ${agent});
		AGI(/opt/gemeinschaft/dialplan-scripts/agent-login-logout.agi,${user_name},${old_local_agent},logoutall);
		UserEvent(QueueLogoffUI,user: ${user_name},queue: *);
		for(i=0;${i} < ${oqueue_count}; i=${i} + 1){
			queueNr=${oqueue${i}};
			QueueLog(${queueNr},${UNIQUEID},Local/${agent}@agents,REMOVEMEMBER,SIP/${user_name});
		}
		Playback(agent-loggedoff);
		
	} else {
		Playback(beeperr);
	}
	Hangup();
	return;
	
}

macro agent-pause() {
	
	Answer();
	Verbose(1,### Agent on ${user_name} wants to pause);
	AGI(/opt/gemeinschaft/dialplan-scripts/agent-pause-unpause.agi,${user_name},pause);
	
	Wait(0.5);
	Verbose(1,### Is Agent:${agent} );
	Verbose(1,### DB-Stat:  ${agent_pause});
	if ("${agent}" = "true") {
		
		UserEvent(AgentPausedUI,agent: ${user_name});
		Playback(beep);

	} else {
		Playback(beeperr);
	}
	Hangup();
	return;
	
}

macro agent-unpause() {
	
	Answer();
	Verbose(1,### Agent on ${user_name} wants to unpause);
	AGI(/opt/gemeinschaft/dialplan-scripts/agent-pause-unpause.agi,${user_name},unpause);
	Wait(0.5);
	
	if ("${agent}" = "true") {
		
		UserEvent(AgentUnPausedUI,agent: ${user_name});

		Playback(beep);
		
	} else {
		Playback(beeperr);
	}
	Hangup();
	return;
	
}


//---------------------------------------------------------------------
//  Agent Login/Logout
//---------------------------------------------------------------------

context agents {

	*6*  => &agent-logout();
	*6   => &agent-login-enterid();
	_*6. => &agent-login(${EXTEN:2});		
	*6#* => &agent-pause();
	*6## => &agent-unpause();

}

//---------------------------------------------------------------------
//  Recordings
//---------------------------------------------------------------------

macro rec-playback(id) {

	Answer();
	Wait(1.0);
	AGI(/opt/gemeinschaft/dialplan-scripts/sys-rec-getfile,${id});
	Playback(/opt/gemeinschaft/sys-rec/${md5hashname});
	Hangup();
	return;

}

macro rec-record(extension) {

	Set(filename=/tmp/${UNIQUEID});
	Set(response=0);
	Answer();
	Wait(1.0);

	while ("${response}" != "1") {
		if ("${response}" = "2") {
			Playback(${filename});
		} else {
			Playback(vm-rec-temp);
			Playback(beep);
			Set(starttime=${STRFTIME(${EPOCH},,%Y%m%d%H%M%S)});
			Record(${filename}.sln);
			Set(endtime=${STRFTIME(${EPOCH},,%Y%m%d%H%M%S)});
		}
		Read(response,/opt/gemeinschaft/sounds/${gs_lang}/druecken-sie-1-um-die-aufnahme-zu-speichern,1,,3,);
	}

	AGI(/opt/gemeinschaft/dialplan-scripts/sys-rec-record,${extension},${filename}.sln,$[${endtime} - ${starttime}]);
	Playback(/opt/gemeinschaft/sounds/${gs_lang}/vielen-dank);
	Hangup();
	return;

}

context sys-rec-playback {

	s => &rec-playback(${ID});

}

context sys-rec-record {

	s => &rec-record(${DIALEDEXTEN});

}

//---------------------------------------------------------------------
//  Conferences (MeetMe)
//---------------------------------------------------------------------

context to-conferences {
	
	_88000 => {
		Set(confprefix=${EXTEN:0:2});
		jump c-empty@to-conferences;
	}
	_880000 => {
		Set(confprefix=${EXTEN:0:2});
		jump c-empty@to-conferences;
	}
	c-empty => {
		Set(confnum=0);
		Verbose(1,### Trying to enter new empty conference);
		&pass-connid();
		if ("${user_id}" != "") {
			// from internal user, store dial log (out)
			&dial-log-store(${user_name},out,${confprefix}${confnum},0);
		}
		// try at most 40 times:
		for (i=0; ${i}<40; i=${i}+1) {
			Set(confnum=${RAND(100,999)});
			Verbose(1,### Searching empty conference - trying ${confprefix}${confnum});
			// find where the conference is or claim it:
			AGI(/opt/gemeinschaft/dialplan-scripts/conf-get-where.agi,${confprefix}${confnum});
			if ("${exttype}" != "conference") {
				Verbose(1,### Something bad happened. Conference ${confprefix}${confnum} is of type "${exttype}");
				Congestion(3);
				Hangup();
			}
			if ("${dial_to_node}" = "") {
				// found an empty conference
				Verbose(1,### Found an empty conference: ${confprefix}${confnum});
				Playback(conf-enteringno);
				SayDigits(${confprefix}${confnum});
				//MeetMe(${confnum},DcoT);
				//Hangup();
				jump ${confprefix}${confnum}@to-conferences-self;
			}
		}
		Verbose(1,### Tried $i{i} times but could not find empty conference);
		Playback(beeperr);
		Congestion(3);
		Hangup();
	}
	
	_88XXX => {
		Set(confprefix=${EXTEN:0:2});
		jump c-${EXTEN:2}@to-conferences;
	}
	_88XXXX => {
		Set(confprefix=${EXTEN:0:2});
		jump c-${EXTEN:2}@to-conferences;
	}
	_c-XX. => {
		Set(confnum=${EXTEN:2});
		Verbose(1,### Call to conference ${confprefix}${confnum});
		&pass-connid();
		if ("${user_id}" != "") {
			// from internal user, store dial log (out)
			&dial-log-store(${user_name},out,${confprefix}${confnum},0);
		}
		// find where the conference is or claim it:
		AGI(/opt/gemeinschaft/dialplan-scripts/conf-get-where.agi,${confprefix}${confnum});
		if ("${exttype}" != "conference") {
			Verbose(1,### Something bad happened. Conference ${confprefix}${confnum} is of type "${exttype}");
			Congestion(3);
			Hangup();
		}
		if ("${dial_to_node}" = "") {
			Verbose(1,### Conference ${confprefix}${confnum} is at node: SELF);
			jump ${confprefix}${confnum}@to-conferences-self;
		}
		else {
			&dial-node(${confprefix}${confnum}@${dial_to_node});
			&hangup-proper-status();
		}
	}
	
}

context to-conferences-self {
	
	/*
	_88000 => {
		Set(confprefix=${EXTEN:0:2});
		jump c-empty@to-conferences;
	}
	*/
	_88XXX => {
		Set(confprefix=${EXTEN:0:2});
		jump c-${EXTEN:2}@to-conferences-self;
	}
	_88XXXX => {
		Set(confprefix=${EXTEN:0:2});
		jump c-${EXTEN:2}@to-conferences-self;
	}
	/*
	c-empty => {
		Verbose(1,### Trying to enter new empty conference);
		//MeetMe(${confnum},eD);
		Answer();
		for (i=100; ${i}<999; i=${i}+1) {
			Verbose(1,### Trying to find empty conference - ${i});
			MeetMeCount(${i},cnt);
			if (${cnt} < 1) {
				Verbose(1,### Found empty conference: ${i});
				Playback(conf-enteringno);
				SayDigits(${confprefix}${i});
				MeetMe(${i},DcoT);
				Hangup();
			}
		}
		Verbose(1,### Could not find empty conference);
		Playback(beeperr);
		Congestion(3);
		Hangup();
	}
	*/
	_c-XX. => {
		Set(confnum=${EXTEN:2});
		Verbose(1,### Entering conference ${confprefix}${confnum});
		&pass-connid();
		Answer();
		Wait(0.5);
		MeetMe(${confnum},DcoT);
		Hangup();
	}
	
}


//---------------------------------------------------------------------
//  Queue Login/Logout
//---------------------------------------------------------------------

macro queue-login( queue ) {
	
	Set(CDR(amaflags)=OMIT);
	Answer();
	Verbose(1,### User ${user_name} wants to log in to queue ${queue});
	
	AGI(/opt/gemeinschaft/dialplan-scripts/queue-login-logout.agi,${user_name},${queue},login);
	if ("${agent_login_status}" = "loggedin") {
		QueueLog(${queue},${UNIQUEID},${CHANNEL(channeltype)}/${user_name},AGENTLOGIN,fake);
	}
	Verbose(1,### agent_login_status: ${agent_login_status});
	Wait(0.5);
	if ("${agent_login_status}" = "loggedin" || "${agent_login_status}" = "alreadyon") {
		UserEvent(QueueLoginUI,user: ${user_name},queue: ${queue});
		Playback(agent-loginok);
	} else {
		Playback(beeperr);
	}
	if ("${agent_login_status}" = "agent-exists") {
		Wait(0.5);
		Playback(/opt/gemeinschaft/sounds/${gs_lang}/an-diesem-telefon-ist-ein-agent-angemeldet);
	}
	Hangup();
	return;
	
}

macro queue-logout( queue ) {
	
	Set(CDR(amaflags)=OMIT);
	Answer();
	Verbose(1,### User ${user_name} wants to log out from queue ${queue});
	AGI(/opt/gemeinschaft/dialplan-scripts/queue-login-logout.agi,${user_name},${queue},logout);
	Wait(0.5);
	if ("${agent_login_status}" = "loggedout") {
		UserEvent(QueueLogoffUI,user: ${user_name},queue: ${queue});
		QueueLog(${queue},${UNIQUEID},${CHANNEL(channeltype)}/${user_name},AGENTLOGOFF,fake);
		Playback(agent-loggedoff);
	} else {
		Playback(beeperr);
	}
	if ("${agent_login_status}" = "agent-exists") {
		Wait(0.5);
		Playback(/opt/gemeinschaft/sounds/${gs_lang}/an-diesem-telefon-ist-ein-agent-angemeldet);
	}
	Hangup();
	return;
	
}

macro queue-logout-all() {
	
	Set(CDR(amaflags)=OMIT);
	Answer();
	Verbose(1,### User ${user_name} wants to log out from all queues);
	AGI(/opt/gemeinschaft/dialplan-scripts/queue-login-logout.agi,${user_name},0,logoutall);
	Wait(0.5);
	if ("${agent_login_status}" = "loggedout") {
		for(i=0;${i} < ${oqueue_count}; i=${i} + 1){
			queueNr=${oqueue${i}};
			QueueLog(${queueNr},${UNIQUEID},${CHANNEL(channeltype)}/${user_name},AGENTLOGOFF,fake);
		}
		UserEvent(QueueLogoffUI,user: ${user_name},queue: *);
		Playback(agent-loggedoff);
	} else {
		Playback(beeperr);
	}
	if ("${agent_login_status}" = "agent-exists") {
		Wait(0.5);
		Playback(/opt/gemeinschaft/sounds/${gs_lang}/an-diesem-telefon-ist-ein-agent-angemeldet);
	}
	Hangup();
	return;
	
}

macro queue-logout-all-silent() {
	// like &queue-logout-all() but silent
	Set(CDR(amaflags)=OMIT);
	Verbose(1,### User ${user_name} wants to log out from all queues);
	AGI(/opt/gemeinschaft/dialplan-scripts/queue-login-logout.agi,${user_name},0,logoutall);
	UserEvent(QueueLogoffUI,user: ${user_name},queue: *);
	return;
}

context queues-login-logout {
	
	*5*   => &queue-logout-all();
	_*5.  => &queue-login(${EXTEN:2});
	//_*5.* => &queue-logout(${EXTEN:2});  // does not work
	_*5X* => &queue-logout(${EXTEN:2:-1});
	_*5XX* => &queue-logout(${EXTEN:2:-1});
	_*5XXX* => &queue-logout(${EXTEN:2:-1});
	_*5XXXX* => &queue-logout(${EXTEN:2:-1});
	_*5XXXXX* => &queue-logout(${EXTEN:2:-1});
	_*5XXXXXX* => &queue-logout(${EXTEN:2:-1});
}


//---------------------------------------------------------------------
//  Voicemailbox
//---------------------------------------------------------------------

// Voicemail abfragen mit eigener Mailbox

context voicemail-self {
	
	voicemail => {
		//Set(CHANNEL(language)=en);  // derzeit notwendig da der dt. Sprachbaustein "eine" im Englischen fehlt
		Set(CDR(amaflags)=OMIT);
		Answer();
		Wait(0.5);
		#exec "/opt/gemeinschaft/etc/asterisk/e-vm-ask-pin.php"
	}
	asterisk  => jump voicemail;
	mailbox   => jump voicemail;
	#include "/opt/gemeinschaft/etc/asterisk/e-number-vmm-self.ael"
}

// Voicemail abfragen mit beliebiger Mailbox

context voicemail-any {
	
	//_80. => {
	_vm-. => {
		Set(vmbox=${EXTEN:3});
		Set(CDR(amaflags)=OMIT);
		Verbose(1,### Check mailbox ${vmbox});
		&check-valid-extension(${vmbox});
		if ("${dial_to_node}" = "") {
			Verbose(1,### VM box ${vmbox} is at node: SELF);
			jump vm-${vmbox}@voicemail-any-self;
		}
		else {
			&dial-node(vm-${vmbox}@${dial_to_node});
			&hangup-proper-status();
		}
	}
	#include "/opt/gemeinschaft/etc/asterisk/e-number-vmm-any.ael"
	
}

context voicemail-any-self {
	
	_vm-. => {
		Set(vmbox=${EXTEN:3});
		Set(CDR(amaflags)=OMIT);
		Verbose(1,### Check mailbox ${vmbox} at this node);
		//Set(CHANNEL(language)=en);  // derzeit notwendig da der dt. Sprachbaustein "eine" im Englischen fehlt
		Answer();
		Wait(0.5);
		VoiceMailMain(${vmbox});
	}
	
}




//---------------------------------------------------------------------
//  Macro to Record VM Box Announcements
//---------------------------------------------------------------------

macro vm-record-file ( ) {
	
	if ("${msource}" = "internal" || "${msource}" = "external") {
		Answer();
		Wait(0.5);
		Playback(vm-extension);
		SayDigits(${user_name});
		Playback(vm-rec-unv);
		Record(/tmp/${user_name}-${msource}.alaw,5,300);
		AGI(/opt/gemeinschaft/dialplan-scripts/vm-rec-move.agi,${user_name},${msource});
		if( "${vm_rec_moved}" = "1" ) {
			Playback(vm-msgsaved);
			Playback(/opt/gemeinschaft/vm-rec/${user_name}-${msource});
		}
		else {
			Playback(/opt/gemeinschaft/sounds/${gs_lang}/aufzeichnen_fehler);
		} 
		Wait(1);
	}
	Hangup();
	return;
}



macro vm-record() {
	
	if ("${msource}" = "internal" || "${msource}" = "external") {
		AGI(/opt/gemeinschaft/dialplan-scripts/vm-rec-exists.agi,${user_name},${msource});
		if( "${vm_rec_exists}" = "1" ) 
			jump s@vm-ann-main-full;
		else
			&vm-record-file();

	}
	Hangup();
	return;
}





macro vm-play-file() {
	
	if ("${msource}" = "internal" || "${msource}" = "external") {
		Playback(/opt/gemeinschaft/vm-rec/${user_name}-${msource});
		Wait(0.5);
	}
	return;

}

macro vm-remove-file() {
	
	if ("${msource}" = "internal" || "${msource}" = "external") {
		AGI(/opt/gemeinschaft/dialplan-scripts/vm-rec-remove.agi,${user_name},${msource});
		if( "${vm_rec_removed}" = "1" ) { 
			Playback(/opt/gemeinschaft/sounds/${gs_lang}/ansage_geloescht);
		}
		else {
			Playback(/opt/gemeinschaft/sounds/${gs_lang}/loeschen_fehler);
		}
		
		Playback(/opt/gemeinschaft/sounds/${gs_lang}/auf-wiedersehen);
		Hangup();	

	}
	Hangup();
	return;

}


//---------------------------------------------------------------------
//  Record VM Box Announcements
//---------------------------------------------------------------------



context vm-ann-main-full {
	 s => {
		&vm-play-file();
		Wait(0.5);
		Background(/opt/gemeinschaft/sounds/${gs_lang}/1_aufnahme_nicht_erreichbar);
		Wait(0.2);
		Background(/opt/gemeinschaft/sounds/${gs_lang}/2_ansage_loeschen);
		Wait(0.2);
		Background(/opt/gemeinschaft/sounds/${gs_lang}/raute_taste_verlassen);
		WaitExten(10);
	}

	h => Hangup();

	i => goto #|1;

	t => goto #|1;

	# => {
		Playback(/opt/gemeinschaft/sounds/${gs_lang}/auf-wiedersehen);
		Hangup();
	}

	1 => &vm-record-file();

	2 => goto vm-ann-del-ok|s|1;

}

context vm-ann-del-ok {
	
	s => {
		Background(/opt/gemeinschaft/sounds/${gs_lang}/loeschen_ok);
		WaitExten(10);
	}

	h => Hangup();

	i => goto t|1;

	t => {
		Playback(/opt/gemeinschaft/sounds/${gs_lang}/auf-wiedersehen);
		Hangup();
	}

	# => goto t|1;

	* => &vm-remove-file();


}

context vm-ann-rec {
	
	vm_rec_internal => {
		Set(CDR(amaflags)=OMIT);
		Set(__msource=internal);
		Answer();
		Wait(0.5);
		&vm-record();
		Hangup();
	}
	vm_rec_external => {
		Set(CDR(amaflags)=OMIT);
		Set(__msource=external);
		Answer();
		Wait(0.5);
		&vm-record();
		Hangup();
	}
	#include "/opt/gemeinschaft/etc/asterisk/e-number-vm-rec-internal.ael"
	#include "/opt/gemeinschaft/etc/asterisk/e-number-vm-rec-external.ael"
}

//---------------------------------------------------------------------
//  Record VM Box -> New multiple ones
//---------------------------------------------------------------------

context vm-rec-multiple {
	
	webdialrecord => {
		Set(CDR(amaflags)=OMIT);
		Answer();
		Wait(0.5);
		//Playback(vm-extension);
		//SayDigits(${user_name});
		Playback(vm-rec-unv);
		Record(/opt/gemeinschaft/vm-rec/${record_file}.alaw,5,300);
		Playback(vm-msgsaved);
		Playback(/opt/gemeinschaft/vm-rec/${record_file});
		Wait(1);
		Hangup();
	}
}



//---------------------------------------------------------------------
//  To Internal Users/Nobodies
//---------------------------------------------------------------------

#include "/opt/gemeinschaft/etc/asterisk/e-internal.ael"


//---------------------------------------------------------------------
//  From the gateways
//---------------------------------------------------------------------

// see hangup_cause2sip() in chan_sip.c for a list of Q.931 cause
// codes (see causes.h) which can be translated into SIP codes

// wrapper context to be used by all of the gateways
context from-gateways {
	
	includes {
		cmm;                  // user login/logout (for BOI)
		to-internal-custom;
		//voicemail-any;        // voicemail main (own mailbox)
		//to-internal-nobody;   // internal phones (nobodies)
		//ivrs;                 //FIXME IVR demo
		to-internal-users;    // internal phones (users)
	}
	i => {
		// no matching extension found in any of the included contexts.
		// this is unlikely to happen as there is an extension for the
		// internal users which catches almost anything.
		Verbose(1,### No matching extension found for "${did_number}");
		// 1 = AST_CAUSE_UNALLOCATED => SIP 404 Not Found
		Set(PRI_CAUSE=1);
		Hangup(1);
	}
}

// contexts for incoming calls from the gateway groups:
#exec "/opt/gemeinschaft/etc/asterisk/e-gategroups-in.ael.php"


//---------------------------------------------------------------------
//  To external
//---------------------------------------------------------------------

context to-gateway {

	_0. => {
		if ("${user_id}" != "") {
			// from internal user, store dial log (out)
			//DumpChan();
			&dial-log-store(${user_name},out,${EXTEN},0);
		}
		&dial-gateway(${gateway_prefix}${EXTEN:1});
	}
	//_X. => &dial-gateway(${EXTEN});
}

macro dial-gateway-do( mnumber ) {
	// get CLIR for a call to external:
	&is_dmz(${mnumber});
	if ("${is_dmz}" != "1") {
		Set(zone=external);
	}
	else {
		Set(zone=internal);
	}
	if ("${user_id}" != "") {
		AGI(/opt/gemeinschaft/dialplan-scripts/get_callerid.agi,${user_name},${zone});
		if ("${clir}" = "" ) {
			AGI(/opt/gemeinschaft/dialplan-scripts/get-clir-${zone}.agi,${user_id});
		}
	}
	if ("${external_callerid_override}" = "true") {
		AGI(/opt/gemeinschaft/dialplan-scripts/out-route.agi,${mnumber},${is_sub_system},${CALLERID(num)},${user_id},1);
	}
	else if ("${is_forwarded}" = "1") {
		// Hack to use clip no screening properly
		if ("${CALLERID(num):0:2}" = "00")
			Set(CALLERID(num)=${CALLERID(num):2});
		AGI(/opt/gemeinschaft/dialplan-scripts/out-route.agi,${mnumber},${is_sub_system},${CALLERID(num)},${user_id},1);
	}
	else {
		AGI(/opt/gemeinschaft/dialplan-scripts/out-route.agi,${mnumber},${is_sub_system},${user_name},${user_id},0);
	}
	Set(no_route=1);
	Set(r=1);
	Set(r_x_cid_name=${CALLERID(name)});
	while ("${r_${r}_dial}" != "") {
		//Verbose(1,### route: r_${r});
		Set(no_route=);
		if ("${clir}" != "1") {
			// RFC 2543:
			Set(CALLERID(num)=${r_${r}_cid});
			Set(CALLERID(name)=${r_x_cid_name});
			if (${LEN(${CALLERID(name)})} < 1) {
				Set(CALLERID(name)=${CALLERID(num)});
			}
			// RFC 3325:
			SIPAddHeader(P-Asserted-Identity: ${QUOTE(${URIENCODE(${CALLERID(name)})})} <sip:${CALLERID(num)}@${r_${r}_cid_host}>);
			SIPAddHeader(P-Preferred-Identity: ${QUOTE(${URIENCODE(${CALLERID(name)})})} <sip:${CALLERID(num)}@${r_${r}_cid_host}>);
			// RFC 3325, RFC 3323:
			SIPAddHeader(Privacy: none);
			// Set caller presentation - affects Remote-Party-ID header: ...";privacy:off;screen:yes"
			Set(CALLERPRES()=allowed_passed_screen);
		}
		else {
			// RFC 2543:
			Set(CALLERID(num)=anonymous);
			Set(CALLERID(name)=Anonymous);
			// RFC 3325:
			SIPAddHeader(P-Preferred-Identity: ${QUOTE(${URIENCODE(${CALLERID(name)})})} <sip:${CALLERID(num)}@${r_${r}_cid_host}>);
			// RFC 3325, RFC 3323:
			SIPAddHeader(Privacy: id;header);
			// Set caller presentation - affects Remote-Party-ID header: ...";privacy:full;screen:yes"
			SetCallerPres(prohib_passed_screen);
		}
		Verbose(1,### Outbound route: ${r_${r}_dial} - Caller ID: "${CALLERID(name)}" <${CALLERID(num)}@${r_${r}_cid_host}>);
		Dial(${r_${r}_dial},180);
		Verbose(1,### Dialstatus for ${r_${r}_dial}: ${DIALSTATUS});
		if ("${DIALSTATUS}" = "ANSWER" || "${DIALSTATUS}" = "CANCEL") {
			Hangup();
		}
		else if ("${DIALSTATUS}" = "NOANSWER") {
			Hangup();
		}
		else if ("${DIALSTATUS}" = "BUSY") {
			Busy(5);
			Hangup();
		}
		else if ("${DIALSTATUS}" = "CHANUNAVAIL" || "${DIALSTATUS}" = "CONGESTION") {
			// see Asterisk's causes.h or
			// Q.931 Disconnect Cause Codes
			if ("${HANGUPCAUSE}" = "1") {
				// 1 = unallocated number
				Verbose(1,### HANGUPCAUSE: 1 = unallocated number);
				Hangup();
			}
			else if ("${HANGUPCAUSE}" = "66") {
				// 66 = channel not implemented
				Verbose(1,### HANGUPCAUSE: 66 = channel not implemented);
				Verbose(0,### You're trying to dial to channel type "${CUT(r_${r}_dial,/,1)}" which this Asterisk is not configured to handle!);
			}
			else if ("${HANGUPCAUSE}" = "17") {
				// 17 = user busy
				Verbose(1,### HANGUPCAUSE: 17 = user busy);
				Busy(5);
				Hangup();
			}
			else if ("${HANGUPCAUSE}" = "18") {
				// 18 = no user responding
				Verbose(1,### HANGUPCAUSE: 18 = no user responding);
				Congestion(5);
				Hangup();
			}
			else if ("${HANGUPCAUSE}" = "127") {
				// 127 = interworking, unspecified
				Verbose(1,### HANGUPCAUSE: 127 = interworking or unspecified);
				Hangup();
			}
			else {
				Verbose(1,### HANGUPCAUSE: ${HANGUPCAUSE});
			}
			
			// else don't hangup and try the next gateway
		}
		else {
			Congestion(5);
			Hangup();
		}
		
		if (${r} > 10) {
			Verbose(1,### Could not reach ${mnumber} via 10 different gateways);
			Congestion(5);
			Hangup();
		}
		r=${r}+1;
		
		// Remove all custom headers (P-Preferred-Identity, Privacy, ... - see above):
		&sip-remove-header(${});
	}
	if ("${no_route}" != "") {
		Verbose(1,### No matching route for ${mnumber} configured);
		Set(DIALSTATUS=CONGESTION);
		Congestion(5);
		Hangup();
	}
	else {
		// we have run out of gateways to try
		Congestion(5);
		Hangup();
	}
	Hangup();
	return;
}

macro dial-gateway( mnumber ) {
	
	// is the user allowed to dial that number?
	// always allow for private calls:
	if ("${is_private_call}" = "1")
		goto allowed;
	// else check:
	
	if ("${forwarded_by}" = "") {
		Set(__is_forwarded=0);
		Set(__forwarded_by=${user_name});
	}
	else {
		Set(__is_forwarded=1);
		Verbose(1,### forwarded_by: ${forwarded_by});
	}
	// AGI ...      dial_allowed = yes | no | pin,  dial_pin = <pin>
	AGI(/opt/gemeinschaft/dialplan-scripts/callblocking.agi,${forwarded_by},${mnumber});
	if ("${dial_allowed}" = "pin") {
		Verbose(1,### User ${user_name} needs PIN (${dial_pin}) to dial ${mnumber} via gateway);
		if ("${is_forwarded}" = "1") {
			Verbose(1,### Don't ask for PIN after forward. Hangup.);
			goto not_allowed;
		}
		Answer();
		Wait(1);
		Read(input,vm-password,10,s,3,5);
		if ("${input}" = "${dial_pin}") {
			Playback(beep);
			ResetCDR(v);
			goto allowed;
		}
		goto not_allowed;
	}
	if ("${dial_allowed}" != "no")
		goto allowed;
	goto not_allowed;
	
	not_allowed:
		Verbose(1,### User ${forwarded_by} is not allowed to dial ${mnumber} via gateway);
		// beeperr if channel is up, else just declined:
		if ("${CHANNEL(state)}" = "Up")
			Playback(beeperr);
		Hangup();
		return;
	
	allowed:
		Verbose(1,### User ${forwarded_by} is allowed to dial ${mnumber} via gateway);
		//&dial-gateway-do(${gateway_prefix}${mnumber});
		&dial-gateway-do(${mnumber});
		Hangup();
		return;

	return;
}


//---------------------------------------------------------------------
//  To Emergency Numbers
//---------------------------------------------------------------------

context to-emergency {
	
	#exec "/opt/gemeinschaft/etc/asterisk/e-emergency.ael.php"
}

macro emergency-call( mnumber ) {
	if ("${mnumber}" = "${emergency_police}") {
		Set(tmp=(police));
	} else if ("${mnumber}" = "${emergency_fire}") {
		Set(tmp=(fire dept.));
	} else {
		Set(tmp=);
	}
	Verbose(1,### Call to ${MACRO_EXTEN} mapped to emergency ${mnumber} ${tmp});
	&dial-gateway-do(${mnumber});
	return;
}


//---------------------------------------------------------------------
//  Private Calls
//---------------------------------------------------------------------

context private-call {
	
	_*7*. => {
		Set(number=${EXTEN:3});
		
		if ("${user_id}" != "") {
			// from internal user, store dial log (out)
			&dial-log-store(${user_name},out,${EXTEN},0);
		}
		
		// pruefen, ob der User Privatgespraeche machen darf ...
		
		// accountcode setzen ...
		AGI(/opt/gemeinschaft/dialplan-scripts/usercode-by-ext.agi,${user_name});
		Set(CDR(amaflags)=BILLING);
		Set(CDR(accountcode)=prv-${user_code});
		
		// fuer Amtsberechtigung:
		Set(__is_private_call=1);
		Verbose(1,### Private call from user ${user_name} to ${number});
		//&dial-gateway(${number});
		Dial(Local/${number}/n);
		&hangup-proper-status();
	}
	//_96. => {
	//	jump *7*${EXTEN:2};
	//}
	//FIXME - exec e-number-private.ael.php here
}


//---------------------------------------------------------------------
//  User Config of Line Features and Call Forwarding
//---------------------------------------------------------------------

#include "/opt/gemeinschaft/etc/asterisk/e-user-config.ael"


//---------------------------------------------------------------------
//  From Internal Users
//---------------------------------------------------------------------

context from-internal-nobody {
	
	includes {
		//ivrs;                 //FIXME IVR demo
		to-emergency;         // emergency numbers
		cmm;                  // user login/logout
		voicemail-any;        // voicemail main (any mailbox)
		to-conferences;       // conferences
		to-internal-nobody;   // internal phones (nobodies)
		to-internal-users;    // internal phones (users)
		test;                 // test extensions
	}
	
	_*31X. => {
		Verbose(1,### Set CLIR for number ${EXTEN:3});
		Set(__clir=1);
		jump ${EXTEN:3}@from-internal-nobody;
	}
	
}

context from-internal-users {
	
	includes {
		//ivrs;                 //FIXME IVR demo
		to-emergency;         // emergency numbers
		cmm;                  // user login/logout
		from-internal-custom;
		to-internal-custom;
		vm-ann-rec;           // record vm-box announcements
		voicemail-self;       // voicemail main (own mailbox)
		voicemail-any;        // voicemail main (any mailbox)
		private-call;         // private calls
		user-config;          // user's configuration
		queues-login-logout;  // queue login/logout
		to-conferences;       // conferences
		to-internal-nobody;   // internal phones (nobodies)
		to-internal-users;    // internal phones (users)
		//to-gateway;           // to the gateway
		test;                 // test extensions
		agents;
	}
	
	_*31X. => {
		Verbose(1,### Set CLIR for number ${EXTEN:3});
		Set(__clir=1);
		jump ${EXTEN:3}@from-internal-users;
	}
}


//---------------------------------------------------------------------
//  From Fax server
//---------------------------------------------------------------------

context from-fax-server {
	_[*#0-9]. => {
		Set(__user_id=${CALLERID(name)});
		Set(__user_name=${CALLERID(num)});
		//Set(CALLERID(name)=${CALLERID(num)});
		Verbose(1,### Sending fax from user ${CALLERID(num)}, ${user_id} to ${EXTEN});
		jump ${EXTEN}@from-internal-users;
	}
}


//---------------------------------------------------------------------
//  From unauthenticated IAX user
//---------------------------------------------------------------------

context from-iax-unauth {
	9999999999 => {
		Hangup();
	}
}


//---------------------------------------------------------------------
//  From other Nodes
//---------------------------------------------------------------------

context from-node {
	includes {
		to-conferences-self;       // conferences
		to-internal-nobody-self;   // internal phones (nobodies)
		voicemail-any-self;        // vm boxes
		to-internal-users-self;    // internal phones (users)
	}
}


//---------------------------------------------------------------------
//  Macro to store dial log
//---------------------------------------------------------------------

macro dial-log-store( muser, mtype, mnumber, mqueue ) {
	
	if ("${mtype}"="in" || "${mtype}"="out" || "${mtype}"="missed") {
		if ("${mnumber}" = "") {
			if ("${mtype}"="in" || "${mtype}"="missed") {
				Set(mnumber=${CALLERID(num)});
			}
		}

		if ( "${mtype}" = "out" && "${mname}" = "" ) {
			AGI(/opt/gemeinschaft/dialplan-scripts/out-get-callername.agi,${muser},${mnumber});
		}
		else if ( "${mtype}" != "out" && "${mname}" == ""  ) {
			Set(mname=${CALLERID(name)});
		}
		Verbose(1,### Store in dial log of user ${muser}: ${mtype} ${mnumber} ${mname} (queue=${mqueue}));
		TrySystem(/opt/gemeinschaft/dialplan-scripts/dial-log-store '${muser}' '${mtype}' '${mnumber}' '${mqueue}' '${mname}');
	}
	return;
}


//---------------------------------------------------------------------
//  HA-System Alarm
//---------------------------------------------------------------------

context systemalarm {
	
	// wird vom HA-System angerufen falls z.B. ein Node ausfaellt
	systemalarm => {
		Verbose(0,################## SYSTEM-ALARM ##################);
		Set(__is_private_call=1);  // damit Rauswaehlen erlaubt
		/*
		Set(i=1);
		while ("${systemalarm_${i}}" != "") {
			Verbose(0,######### SYSTEM-ALARM - i=${i} - dial ${systemalarm_${i}});
			!!!!!!!
			Dial(Local/${systemalarm_${i}}/n,20,g); // hier noch irgendwas mit Makro ...
			i=${i}+1;
		}
		*/
		Hangup();
	}
	
}


//---------------------------------------------------------------------
//  Call Completion (Autom. Rueckruf)
//---------------------------------------------------------------------

context call-completion {
	
	_cc-. => {
		Set(callee=${EXTEN:3});
		//Set(CHANNEL(language)=de);
		#exec "/opt/gemeinschaft/etc/asterisk/e-lang.ael.php"
		Verbose(1,### Call completion from ${user_name} to ${callee});
		Answer();
		Wait(0.5);
		//SayDigits(${callee});
		//Playback(/opt/gemeinschaft/sounds/${gs_lang}/ist-jetzt-wieder-erreichbar);
		Set(CALLERID(all)=${orig_callerid});
		//DumpChan();
		Set(__is_callcompletion=1); // no forwards and no mailbox
		Dial(Local/${callee}/n,15);
		&hangup-proper-status();
	}
	
}


//---------------------------------------------------------------------
//  dialed with HTTP GET (call-init.php -> callfile)
//---------------------------------------------------------------------

context urldial {
	
	_X. => jump u-${EXTEN};
	_X  => jump u-${EXTEN};
	_*. => jump u-${EXTEN};
	
	_u-. => {
		Set(callfile_target=${EXTEN:2});
		if ("${saved_callerid}" != "") {
			// __saved_callerid is set by call-init.php
			Set(CALLERID(all)=${saved_callerid});
			Set(__saved_callerid=);
		}
		Verbose(1,### Callfile from ${callfile_from_user} to ${callfile_target});
		&pass-connid();
		Set(__is_callfile_origin=);
		Set(__is_call_from=internal);
		Dial(Local/${callfile_target}@from-internal-users/n,200);
		&hangup-proper-status-urldial();
	}
	
	failed => {
		Verbose(1,### Callfile: Origin user ${callfile_from_user} did not answer);
		NoCDR();
		Hangup();
	}
	
}


//--------------------------------------------------------------------
//  Set the Custom-Hints if there is set an callforward from the GUI
//--------------------------------------------------------------------

context toggle-cfwd-hint {
	toggle => {
		//toggle callforwarding
		AGI(/opt/gemeinschaft/dialplan-scripts/in-user-get-fw.agi,${user_name});
		if ("${fw_std_active}" = "1") {
			Set(CDR(amaflags)=OMIT);
			TryExec(Set(DEVICE_STATE(Custom:${user_name}fwd)=BUSY));
			Hangup();
		} else {
			Set(CDR(amaflags)=OMIT);
			TryExec(Set(DEVICE_STATE(Custom:${user_name}fwd)=NOT_INUSE));
			Hangup();
		}
	}
}

//---------------------------------------------------------------------
//  Test-Extensions
//---------------------------------------------------------------------

context test {
	
	*** => {
		Set(CDR(amaflags)=OMIT);
		Verbose(1,### Echo test for user ${user_name});
		Answer();
		Echo();
		Hangup();
	}
	
	/*
	// Intercom
	_***. => {
		SIPAddHeader(Call-Info: \;answer-after=0);
		Dial(SIP/${EXTEN:3});
	}
	*/
	
}


//---------------------------------------------------------------------
//  For the cluster watchdog
//---------------------------------------------------------------------

context cluster-watchdog {
	// check-alive extension for the cluster watchdog:
	checkalive => {
		Set(CDR(amaflags)=OMIT);
		NoOp();
	}
}



//---------------------------------------------------------------------
//  Default Context (do not use here. it's in extensions.conf)
//---------------------------------------------------------------------

/*
context default {
}
*/

//---------------------------------------------------------------------
//  Contexts for IVRs
//---------------------------------------------------------------------

#exec "/opt/gemeinschaft/etc/asterisk/e-ivr.ael.php"


//---------------------------------------------------------------------
//  custom contexts
//---------------------------------------------------------------------

#include "/etc/gemeinschaft/asterisk/extensions-to-internal-custom.ael"

#include "/etc/gemeinschaft/asterisk/extensions-from-internal-custom.ael"


